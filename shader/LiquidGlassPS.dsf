//STAGE:ps
#include "Common.dsh"
#include "shared/LiquidGlass.h"
#include "CalcSDF.dsh"

static const float M_E = 2.718281828459045;

Texture2D src_tex;

cbuffer cb_LiquidGlassParam
{
	LiquidGlassParam param;
}

float f(float x)
{
	return 1.0 - param.refraction_b * pow(param.refraction_c * M_E, -param.refraction_d * x - param.refraction_a);
}

float4 main(
	in float2 iTc : TEXCOORD) : SV_Target
{
	float2 center = float2(0.5, 0.5);
	float2 p = (iTc - center) * 2.0;
	float r = 1.0;
	float d = sdSuperellipse(p, param.shape_power, r);
	if (d > 0)
		discard;
	
	float dist = -d;
	float2 sampleP = p * pow(f(dist), param.refraction_power);
	
	float2 quadNDC2ScreenNDCScale = float2(1.0, 1.0);
	float2 mid_point = float2(0.0, 0.0);
	float2 targetNDC = sampleP * quadNDC2ScreenNDCScale + mid_point;
	float2 coord = targetNDC * 0.5 + float2(0.5, 0.5);
	
	if (max(coord.x, coord.y) > 1.0 || min(coord.x, coord.y) < 0.0)
		return float4(1.0, 0.0, 1.0, 1.0);

	float4 color = src_tex.Sample(linear_sampler, coord);
	return color;
}